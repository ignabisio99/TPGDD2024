USE [GD1C2024]
GO

IF NOT EXISTS (SELECT * FROM sys.schemas WHERE name = 'QUICK_SORT')
	EXEC('CREATE SCHEMA QUICK_SORT')
GO

IF NOT EXISTS(SELECT name FROM sys.procedures WHERE name = 'DROP_TABLES')
	EXEC('CREATE PROCEDURE [QUICK_SORT].[DROP_TABLES] AS BEGIN SET NOCOUNT ON; END');
GO

ALTER PROCEDURE [QUICK_SORT].[DROP_TABLES]
AS
BEGIN
	DECLARE @sql NVARCHAR(500) = ''
	
	DECLARE cursorTablas CURSOR FOR
	SELECT DISTINCT 'ALTER TABLE [' + tc.TABLE_SCHEMA + '].[' +  tc.TABLE_NAME + '] DROP [' + rc.CONSTRAINT_NAME + '];'
	FROM INFORMATION_SCHEMA.REFERENTIAL_CONSTRAINTS rc
	LEFT JOIN INFORMATION_SCHEMA.TABLE_CONSTRAINTS tc
	ON tc.CONSTRAINT_NAME = rc.CONSTRAINT_NAME
	WHERE tc.TABLE_SCHEMA = 'QUICK_SORT'

	OPEN cursorTablas
	FETCH NEXT FROM cursorTablas INTO @sql

	WHILE (@@FETCH_STATUS = 0)
	BEGIN
		EXEC sp_executesql @sql
		FETCH NEXT FROM cursorTablas INTO @Sql
	END

	CLOSE cursorTablas
	DEALLOCATE cursorTablas
	
	EXEC sp_MSforeachtable 'DROP TABLE ?', @whereand='AND schema_name(schema_id) = ''QUICK_SORT'' AND o.name NOT LIKE ''BI_%'''
END
GO

EXEC [QUICK_SORT].[DROP_TABLES]
GO


IF NOT EXISTS(SELECT name FROM sys.procedures WHERE name='CREATE_TABLES')
   EXEC('CREATE PROCEDURE [QUICK_SORT].[CREATE_TABLES] AS BEGIN SET NOCOUNT ON; END');
GO

ALTER PROCEDURE [QUICK_SORT].[CREATE_TABLES]
AS
BEGIN

		CREATE TABLE [QUICK_SORT].[PROVINCIA](
			PROVINCIA_ID INTEGER IDENTITY(1,1) PRIMARY KEY,
			PROVINCIA NVARCHAR(255)
		);

		CREATE TABLE [QUICK_SORT].[LOCALIDAD](
			LOCALIDAD_ID INTEGER IDENTITY(1,1) PRIMARY KEY,
			PROVINCIA_ID INTEGER REFERENCES [QUICK_SORT].[PROVINCIA],
			LOCALIDAD NVARCHAR(255)
		);
		
		CREATE TABLE [QUICK_SORT].[DIRECCION](
			DIRECCION_ID INTEGER IDENTITY(1,1) PRIMARY KEY,
			LOCALIDAD_ID INTEGER REFERENCES [QUICK_SORT].[LOCALIDAD],
			DIRECCION NVARCHAR(255)
		);

		CREATE TABLE [QUICK_SORT].[DESCUENTO_MEDIO_PAGO](
			DESCUENTO_MEDIO_PAGO_CODIGO DECIMAL(18,0) PRIMARY KEY,
			--DESCUENTO_PAGO_MEDIO_PAGO_ID INTEGER REFERENCES [QUICK_SORT].[PAGO_MEDIO_PAGO],
			DESCUENTO_FECHA_INICIO DATETIME,
			DESCUENTO_FECHA_FIN DATETIME,
			DESCUENTO_PORCENTAJE DECIMAL(18,2),
			DESCUENTO_TOPE DECIMAL(18,2),
			DESCUENTO_DESCRIPCION NVARCHAR(255)
		); 

		CREATE TABLE [QUICK_SORT].[SUPERMERCADO](
			SUPER_CUIT NVARCHAR(255) PRIMARY KEY,
			SUPER_DIRECCION_ID INTEGER REFERENCES [QUICK_SORT].[DIRECCION],
			SUPER_NOMBRE NVARCHAR(255),
			SUPER_RAZON_SOC NVARCHAR(255),
			SUPER_IIBB NVARCHAR(255),
			SUPER_FECHA_INI_ACTIVIDAD DATETIME,
			SUPER_CONDICION_FISCAL NVARCHAR(255)
		);

		CREATE TABLE [QUICK_SORT].[SUCURSAL](
			SUCURSAL_ID INTEGER IDENTITY(1,1) PRIMARY KEY,
			SUCURSAL_SUPER_CUIT NVARCHAR(255) REFERENCES [QUICK_SORT].[SUPERMERCADO],
			SUCURSAL_DIRECCION_ID INTEGER REFERENCES [QUICK_SORT].[DIRECCION],
			SUCURSAL_NOMBRE NVARCHAR(255)
		);

		CREATE TABLE [QUICK_SORT].[EMPLEADO](
			EMPLEADO_LEGAJO INTEGER IDENTITY(1,1) PRIMARY KEY,
			EMPLEADO_SUCURSAL_ID INTEGER REFERENCES [QUICK_SORT].[SUCURSAL],
			EMPLEADO_NOMBRE NVARCHAR(255),
			EMPLEADO_APELLIDO NVARCHAR(255),
			EMPLEADO_FECHA_REGISTRO DATETIME,
			EMPLEADO_TELEFONO DECIMAL(18,0),
			EMPLEADO_MAIL NVARCHAR(255),
			EMPLEADO_FECHA_NACIMIENTO DATE,
			EMPLEADO_DNI DECIMAL(18,0)
		);
	
END
GO

EXEC [QUICK_SORT].[CREATE_TABLES]
GO

ALTER PROCEDURE [QUICK_SORT].[MIGRAR]
AS
BEGIN
	-- PROVINCIA

	INSERT INTO [QUICK_SORT].[provincia] (PROVINCIA)
		SELECT DISTINCT SUCURSAL_PROVINCIA
		FROM [gd_esquema].[Maestra]
		WHERE SUCURSAL_PROVINCIA IS NOT NULL
		UNION
		SELECT DISTINCT SUPER_PROVINCIA
		FROM [gd_esquema].[Maestra]
		WHERE SUPER_PROVINCIA IS NOT NULL	
		UNION
		SELECT DISTINCT CLIENTE_PROVINCIA
		FROM [gd_esquema].Maestra
		WHERE CLIENTE_PROVINCIA IS NOT NULL

	-- LOCALIDAD

	INSERT INTO [QUICK_SORT].[LOCALIDAD] (PROVINCIA_ID, LOCALIDAD)
		SELECT DISTINCT PROVINCIA_ID, SUPER_LOCALIDAD
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].PROVINCIA ON SUPER_PROVINCIA = PROVINCIA
		WHERE SUPER_LOCALIDAD IS NOT NULL
		UNION
		SELECT DISTINCT PROVINCIA_ID, SUCURSAL_LOCALIDAD
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].PROVINCIA ON SUCURSAL_PROVINCIA = PROVINCIA
		WHERE SUPER_LOCALIDAD IS NOT NULL
		UNION
		SELECT DISTINCT PROVINCIA_ID, CLIENTE_LOCALIDAD
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].[PROVINCIA] ON CLIENTE_PROVINCIA = PROVINCIA
		WHERE CLIENTE_LOCALIDAD IS NOT NULL

	-- DIRECCION

	INSERT INTO [QUICK_SORT].[DIRECCION] (LOCALIDAD_ID, DIRECCION)
		SELECT DISTINCT LOCALIDAD_ID, SUPER_DOMICILIO
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].[LOCALIDAD] ON SUPER_LOCALIDAD = LOCALIDAD
		WHERE SUPER_DOMICILIO IS NOT NULL
		UNION
		SELECT DISTINCT LOCALIDAD_ID, SUCURSAL_DIRECCION
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].[LOCALIDAD] ON SUCURSAL_LOCALIDAD = LOCALIDAD
		WHERE SUCURSAL_DIRECCION IS NOT NULL
		UNION
		SELECT DISTINCT LOCALIDAD_ID, CLIENTE_DOMICILIO
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].[LOCALIDAD] ON CLIENTE_LOCALIDAD = LOCALIDAD
		WHERE CLIENTE_DOMICILIO IS NOT NULL

	-- SUPERMERCADO

	INSERT INTO [QUICK_SORT].[SUPERMERCADO] (SUPER_CUIT, SUPER_DIRECCION_ID, SUPER_NOMBRE,
	SUPER_RAZON_SOC, SUPER_IIBB, SUPER_FECHA_INI_ACTIVIDAD, SUPER_CONDICION_FISCAL)
		SELECT DISTINCT SUPER_CUIT, DIRECCION_ID, SUPER_NOMBRE, SUPER_RAZON_SOC, SUPER_IIBB, SUPER_FECHA_INI_ACTIVIDAD,
		SUPER_CONDICION_FISCAL
		FROM [gd_esquema].[Maestra]
		JOIN [QUICK_SORT].[DIRECCION] ON SUPER_DOMICILIO = DIRECCION
		WHERE SUPER_CUIT IS NOT NULL 

	-- SUCURSAL
	
	INSERT INTO [QUICK_SORT].[SUCURSAL] (SUCURSAL_SUPER_CUIT, SUCURSAL_DIRECCION_ID, SUCURSAL_NOMBRE)
		SELECT DISTINCT s.SUPER_CUIT, d.DIRECCION_ID, m.SUCURSAL_NOMBRE
		FROM [gd_esquema].[Maestra] m
		JOIN [QUICK_SORT].[DIRECCION] d ON m.SUCURSAL_DIRECCION = d.DIRECCION
		JOIN [QUICK_SORT].[SUPERMERCADO] s ON s.SUPER_CUIT = m.SUPER_CUIT
		WHERE SUCURSAL_NOMBRE IS NOT NULL

	-- EMPLEADO (TODO: La FK de sucursal_id quedaria en null?)
	
	INSERT INTO [QUICK_SORT].[EMPLEADO] (EMPLEADO_SUCURSAL_ID, EMPLEADO_NOMBRE, EMPLEADO_APELLIDO, 
	EMPLEADO_FECHA_REGISTRO, EMPLEADO_TELEFONO, EMPLEADO_MAIL, EMPLEADO_FECHA_NACIMIENTO, EMPLEADO_DNI)
		SELECT DISTINCT s.SUCURSAL_ID, m.EMPLEADO_NOMBRE, m.EMPLEADO_APELLIDO, m.EMPLEADO_FECHA_REGISTRO, m.EMPLEADO_TELEFONO,
		m.EMPLEADO_MAIL, m.EMPLEADO_FECHA_NACIMIENTO, m.EMPLEADO_DNI
		FROM [gd_esquema].[Maestra] m
		JOIN [QUICK_SORT].[SUCURSAL] s ON s.SUCURSAL_NOMBRE = m.SUCURSAL_NOMBRE
		WHERE EMPLEADO_DNI IS NOT NULL

	-- DESCUENTO MEDIO PAGO (TODO:FK DESCUENTO PAGO MEDIO PAGO ID)

	INSERT INTO [QUICK_SORT].[DESCUENTO_MEDIO_PAGO](DESCUENTO_MEDIO_PAGO_CODIGO, DESCUENTO_FECHA_INICIO,
	DESCUENTO_FECHA_FIN, DESCUENTO_PORCENTAJE, DESCUENTO_TOPE, DESCUENTO_DESCRIPCION)
		SELECT DISTINCT m.DESCUENTO_CODIGO, m.DESCUENTO_FECHA_INICIO, m.DESCUENTO_FECHA_FIN, m.DESCUENTO_PORCENTAJE_DESC,
		m.DESCUENTO_TOPE, m.DESCUENTO_DESCRIPCION
		FROM [gd_esquema].[Maestra] m
		WHERE m.DESCUENTO_CODIGO IS NOT NULL

END
GO

EXEC [QUICK_SORT].[MIGRAR]
GO

-- SELECT * FROM gd_esquema.Maestra